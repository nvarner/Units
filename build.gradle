buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}


plugins {
    id "java"
    id "maven"
    id "maven-publish"
}

apply plugin: 'com.jfrog.bintray'

def versionTag = "2.0.0"

group "com.nathanvarner.units"
version versionTag

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

// Create the pom configuration:
def pomConfig = {
    inceptionYear "2018"
    licenses {
        license {
            name "MIT"
            url "https://mit-license.org/"
            distribution "repo"
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Create the publication with the pom configuration:
publishing {
    publications {
        UnitsPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.nathanvarner.units'
            artifactId 'units'
            version
            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'units')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty("bintrayUser") ? project.property("bintrayUser") : ""
    key = project.hasProperty("bintrayKey") ? project.property("bintrayKey") : ""
    publish = true
    publications = ["UnitsPublication"]
    pkg {
        repo = "maven"
        name = "units"
        licenses = ["MIT"]
        vcsUrl = "https://gitlab.com/nvarner/units"
        version {
            name = versionTag
            released = new Date()
        }
    }
}

dependencies {
    testCompile group: "junit", name: "junit", version: "4.12"
}

test {
    testLogging {
        events "passed"
    }
}
